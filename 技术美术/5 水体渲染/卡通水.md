---
title: 卡通水
aliases: 
tags: 
create_time: 2023-08-02 13:55
uid: 202308021355
banner: "[[8c71359a0845ad35f2e8feb8b31a627a_MD5.gif]]"
---

[https://roystan.net/articles/toon-water.html](https://roystan.net/articles/toon-water.html)

接下来，我会从几个方面概括实现过程：

1.  基于水深度的水着色
2.  基于 uv 的水波纹动画
3.  采样屏幕空间法线，和观察空间顶点法线点积。实现**垂直与水平**水面的物体泡沫量的单独调整
4.  自定义混合模式，实现可改变的泡沫颜色
5.  用 SmoothStep 代替阈值 cutoff，实现抗锯齿

首先调整为 AplhaBlend 模式
- 渲染队列改为 Transparent
- 混合模式改为：Blend SrcAlpha OneMinusSrcAlpha (不一定，依效果而定)
- 关闭深度写入
- 关闭Cull
# 1 基于深度着色

## 计算水深
水深 = 水下不透明物体在深度纹理中的观察空间深度（深度纹理中采样） -  水面的观察空间深度 ( i.positionCS. w = -positionVS. z）

```c h:1,13,15
//1 水下不透明物体在深度纹理中的观察空间深度
#if UNITY_REVERSED_Z
float opaqueDepth = SampleSceneDepth(ScreenUV);
#else
float opaqueDepth = lerp(UNITY_NEAR_CLIP_VALUE, 1, SampleSceneDepth(ScreenUV));
#endif
float opaqueDepthVS = LinearEyeDepth(opaqueDepth, _ZBufferParams);  //线性
//2 水面的观察空间深度
float waterSurfaceDepth = i.positionCS.w; //或= -i.positionVS.z
//3 计算水深
float WaterDepth = opaqueDepthVS - waterSurfaceDepth;
```

![[Pasted image 20230805153639.png]]

**水上水下的判定：**
![[Pasted image 20230805154127.png]]

## 深度插值着色

*   水和地面距离近时颜色表现为浅色（绿色部分）
*   水和地面距离较远时表现为深色（红色部分）

![[8a678073c4caa7d205a8aa37371d4db0_MD5.png]]

- 用 Lerp 函数对浅水区颜色和深水区颜色进行插值
- 通过调整最大深度值参考，控制近处和远处水颜色
```c
//4 除以最大水深，归一化深度
float waterDepthNormalize = saturate(waterDepth / _MaxDepth);
//5 插值深度着色
float4 waterColor = lerp(_ShallowColor, _DepthColor, waterDepthNormalize);
```

# 2 水波纹

## 1. 使用 Perlin Noise 添加波纹

*   我们使用柏林 noise 在水的表面添加波浪，并且用水深度来控制波浪的可见度（浅水处更明显，产生海岸线效果）

**a. 采样 noise**

```c
//声明个新属性用来读noise图
_SurfaceNoise("Surface Noise", 2D) = "white" {}

// 输入结构增加uv
float4 uv : TEXCOORD0;

// 输出结构增加uv
float2 noiseUV : TEXCOORD0;

// 使用前声明变量
sampler2D _SurfaceNoise;
float4 _SurfaceNoise_ST;//支持Tilling和offset

// 顶点着色器：传递uv，并支持Tilling和Offset
o.noiseUV = TRANSFORM_TEX(v.uv, _SurfaceNoise);

//片元着色器：采样noise
float surfaceNoiseSample = tex2D(_SurfaceNoise, i.noiseUV).r;
```

**b. 风格化波纹**

*   直接采样 noise 的效果是很辣眼睛的，我们需要再进行更卡通的改造
*   // 如果接触过卡渲风格的传统经验关照，一个思路就是将渐变过程截断成 “卡硬” 效果
*   所以这里我们设定一个阈值对上一步采样的 noise 结果进行 cutoff：

*   小于这个阈值的将会被直接舍弃（0）
*   大于这个阈值的将会被设为 1

```c
// Add as a new property.
_SurfaceNoiseCutoff("Surface Noise Cutoff", Range(0, 1)) = 0.777

// Matching property variable.
float _SurfaceNoiseCutoff;

// Add in the fragment shader, just after sampling the noise texture.
float surfaceNoise = surfaceNoiseSample > _SurfaceNoiseCutoff ? 1 : 0;
```

*   **// 阶段展示**  
    此时我们的已经可以通过调整阈值来控制还波纹

![[e4d5bac9d42d1688771e3b830b177ba4_MD5.gif]]

## 2. 与水相交的泡沫
泡沫产生区域：浅水区

思路：
*   基于水深度，调整 noise 的 cutoff 阈值

```c
// Control for what depth the shoreline is visible.
_FoamDistance("Foam Distance", Float) = 0.4

// Matching variable.
float _FoamDistance;

// 片元着色器
float foamDepthDifference01 = saturate(depthDifference / _FoamDistance);
	//泡沫深度
float surfaceNoiseCutoff = foamDepthDifference01 * _SurfaceNoiseCutoff;
	//将泡沫深度乘给cutoff阈值
float surfaceNoise = surfaceNoiseSample > surfaceNoiseCutoff ? 1 : 0;
```

这里有个问题：
*   **水和岸的相交点很棒，但和物体的相交处缺不明显**

![[d71558de32377863358341a23ffcec16_MD5.gif]]

## 3. 水流动、扰动动画

FlowMap 来处理噪声贴图, 让噪声贴图流动起来

```c
//扰动UV
o.noiseUV = TRANSFORM_TEX(float2(i.noiseUV + _TimeSpeed.xy * _Time.y), _SurfaceNoise);

//FlowMap来处理噪声贴图
 float3 flowDir = SAMPLE_TEXTURE2D(_FlowMap, sampler_FlowMap, i.uv).rgb*2.0-1.0;
 flowDir*=_FlowSpeed;
 float phase0 = frac(_Time.y*_TimeSpeed.x);
 float phase1 = frac(_Time.y*_TimeSpeed.x+0.5);
 float tex0 = SAMPLE_TEXTURE2D(_SurfaceNoise, sampler_SurfaceNoise, i.noiseUV-flowDir.xy*phase0).r;
 float tex1 = SAMPLE_TEXTURE2D(_SurfaceNoise, sampler_SurfaceNoise, i.noiseUV-flowDir.xy*phase1).r;
 float NoiseFlowTex = lerp(tex0,tex1,abs((0.5-phase0)/0.5));
```


# 3 解决和漂浮物相交泡沫不明显问题

岸边和水面之间的深度相当小，而水面和水下物体之间的深度（从相机的角度）相对较大。

*   **解决方法**：
    *   **方案 1：**
    *   将_FoamDistance 参数增加到 0.4 左右即
    *   这样会同时改变水岸和漂浮物的泡沫，水岸的泡沫过于多了。

![[edccb30029127ac3971c067e5343ef5b_MD5.png]]

*   **方案 2：**
    *   根据**水面以下的角度**来改变**泡沫的深度（foam depth）**
    *   这样一来，垂直的物体表面（和漂浮物相交部分）可以比平坦的物体表面（岸边）得到更多的泡沫（白色）

接下来就开始施行方案 2，需要用的法线纹理

**a. 计算水面的观察空间法线**

我们需要计算**水的 view space 下的法线**，来和渲染好的法线做比较
```cs
//观察空间法线  
float3 normalVS = TransformWorldToViewNormal(N);
```

> [!NOTE] 理解观察空间法线
> 随着摄像机位移不断变化，Unity 摄像机在观察空间看向-z，所以朝向摄像机的法线都为 z 轴方向，越朝向相机值越接近（0，0，1）
> 
![[202308012045.gif]]

**为什么这会让水面漂浮物的泡沫更多呢？**
深度法线纹理：
![[Pasted image 20230802141329.png|350]]

观察空间法线：
![[Pasted image 20230802141356.png|350]]

**点积结果**
对于交界处的像素，深度发现纹理和观察空间法线趋向于垂直，所以点积结果小。
对于非交界处的像素，深度法线纹理和观察空间法线趋向于平行，所以点积结果大。
![[Pasted image 20230802141427.png|350]]

很直观，交界处点积结果小，非交界处点积结果大。这样就区分出了交界的点积区别。

```cs
float3 NormalsTexture = SampleSceneNormals(ScreenUV); //法线纹理
float3 normalDot = saturate(dot(NormalsTexture, normalVS)); //法线纹理点积观察空间法线
```

我们使用 lerp，让交界处的值对应大值 `_FoamMaxDistance`，让非交界处对应小值 `_FoamMinDistance`。这样计算 `foamDepthDifference` 时交界处的值就小（代表深度差异小）。
```c
float foamDistance = lerp(_FoamMaxDistance, _FoamMinDistance, normalDot);
float foamDepthDifference = saturate(depthDifference / foamDistance);
```

这样计算裁剪值时，交界处的裁剪值就小，所以泡沫更多。
```cs
float FoamNoiseCutoff = foamDepthDifference * _SurfaceNoiseCutoff;
```

![[100235deb9fad05e85d13a2a4dd19645_MD5.gif]]


# 4 抗锯齿

*   仔细观察一下，我们能法线泡沫的边缘实际上锯齿是比较严重的。
*   锯齿严重的原因是我们 cutoff noise 的方式：**不是 0 就是 1**

![[bbd31c5b93b25fdf6cd7183ff4b1518f_MD5.png]]

**解决方法**
*   使用 SmoothStep 函数将 Alpha 从 0 到 1 平滑过渡
*   **我们定义`_FoamEdgeFade` 值必须很小，让 smoothStep 的下限和上限非常接近，0~1 的变化斜率非常大。`_FoamEdgeFade`太大会过于软，没有卡通效果**

```c
//卡通硬边会有锯齿
//float FoamNoise = NoiseFlowTex > FoamNoiseCutoff ? 1 : 0;
//抗锯齿：平滑过渡
float FoamNoise = smoothstep(FoamNoiseCutoff-_FoamEdgeFade,FoamNoiseCutoff+_FoamEdgeFade,NoiseFlowTex);
```


# 5 水下折射

用法线贴图对屏幕 uv 坐标做扭曲
法线有 xyz 三个分量，而噪点只有灰度值，因此法线可以描述在 uv（xy）方向不同的扭曲程度，效果更好（单取 RGB 通道，相当于三张噪声图）
```c
//法线贴图扰动屏幕UV
float3 normalMap = UnpackNormalScale(
    SAMPLE_TEXTURE2D(_NormalMap, sampler_NormalMap, i.normalUV), _NormalScale);
float3x3 TBN = CreateTangentToWorld(i.normalWS, i.tangentWS.xyz, i.tangentWS.w);
float3 N = TransformTangentToWorld(normalMap, TBN, true);
float2 bias = N.xy * _CameraOpaqueTexture_TexelSize.xy * _RefractFactor;
float2 ScreenUVRefract = ScreenUV + bias;
```

对水深做判断，水上用屏幕 UV，水下用扭曲 UV，来采样不透明纹理
```cs
//--------------------------------------------
// 判断水上水下，水上用屏幕UV，水下用扭曲UV，来采样抓屏纹理
//--------------------------------------------
if (waterDepth < 0)
{
    ScreenUVRefract = ScreenUV;
}
float4 OpaqueTexture = SAMPLE_TEXTURE2D(_CameraOpaqueTexture, sampler_CameraOpaqueTexture, ScreenUVRefract);
```

# 6 水面反射
## 高光反射
BlinnPhong

## 环境反射
采样 CubeMap 或反射探针

# 7 焦散

方法一：水面贴焦散贴图，太假
方法二：每个物体贴焦散贴图，麻烦
**方法三：深度重建世界空间坐标，将坐标的 xz 分量作为 uv 坐标对焦散贴图贴图采样**[Rendering realtime caustics --- 渲染实时焦散 (ameye.dev)](https://ameye.dev/notes/realtime-caustics/)
## 焦散体积 caustics volumes
使用焦散的一个好方法是**创建焦散体积 caustics volumes**（即创建一个 box，赋给它材质）。从本质上讲，我们创建了一个 volumes，并将其定位在场景中应显示焦散的位置。
![[Pasted image 20230806145250.png]]
##  Box Volumes


首先深度缓冲重建世界坐标并转换到局部空间

焦散体积的作用类似于贴花 Decal，焦散显示在 Volumes 与场景几何体相交的任何位置。为了实现这一点，我们在局部空间中计算 Box Mask，以限制着色器的输出。
```c
//在局部空间中计算Box Mask
float3 rebuildPosOS = TransformWorldToObject(rebuildPosWS);
//局部空间坐标范围在-0.5到0.5之间，作为Box的边界
//通过将输出与此边界框遮罩相乘，我们可以将焦散限制为仅在需要的地方渲染。
float boundingBoxMask = all(step(rebuildPosOS, 0.5) * (1 - step(rebuildPosOS, -0.5)));
```

`all(x)` ：所有分量是否均为非零，均为非零则返回 true，否则返回 false
![[Pasted image 20230806145901.png|500]]

## 相机进入 volumes
现在的相机进入 box 内后就看不到东西了，为了允许摄影机进入焦散体积内部后看到焦散，我们应用了一个很酷的技巧：
- 使用 Cull Front 只渲染背面
- 我们还使用 ZTest Always 始终渲染这些背面，即使其他场景几何体正在阻挡它。
```cs
Cull Front
ZTest Always
```

## 焦散贴图映射
可以通过将世界空间坐标用作 UV，将焦散映射到场景几何体。如果使用世界空间位置的 xz 分量作为 UV，则焦散将**固定**显示为自上而下，投影到场景几何体上。
**改进：使用主灯光方向来影响用于对焦散纹理进行采样的 UV。焦散将跟随灯光的方向，并看起来像是投影到场景上。**
![[Pasted image 20230806160306.png|500]]

使用脚本获取光方向，计算采样 UV，然后进行采样。

```cs
public class GetLightMatrix : MonoBehaviour
{
    public Material causticsMaterial;
    private Matrix4x4 m_lightMatrix;
    private static readonly int s_MainLightDirection = Shader.PropertyToID("_MainLightDirection");

    void Update()
    {
        //局部空间转换到世界空间的矩阵
        m_lightMatrix = RenderSettings.sun.transform.localToWorldMatrix;
        causticsMaterial.SetMatrix(s_MainLightDirection, m_lightMatrix);
    }
}
```

```c
//主灯光方向影响焦散采样UV
float2 causticsUV = mul(rebuildPosWS,_MainLightDirection).xy;
float4 caustics = SAMPLE_TEXTURE2D(_CausticsTexture, sampler_CausticsTexture, float2((causticsUV + _TimeSpeed * _Time.y) * _CausticsTexture_ST.xy +_CausticsTexture_ST.zw));
```

## 多纹理叠加
![[Pasted image 20230806160324.png]]
只有一个焦散贴图看起来不好看，我们可以在第一个焦散纹理上放置第二个焦散贴图。**这里的技巧是用不同的强度和比例移动纹理。**
```c
float2 causticsUV = mul(rebuildPosWS,_MainLightDirection).xy;

//uv偏移
float2 uv1 = (causticsUV + _TimeSpeed * _Time.y * 0.75) * _CausticsTexture_ST.xy +_CausticsTexture_ST.zw;
float2 uv2 = (causticsUV + _TimeSpeed * _Time.y * 1) * (-_CausticsTexture_ST.xy) +_CausticsTexture_ST.zw;

float4 tex1 = SAMPLE_TEXTURE2D(_CausticsTexture, sampler_CausticsTexture, uv1);
float4 tex2 = SAMPLE_TEXTURE2D(_CausticsTexture, sampler_CausticsTexture, uv2);

float3 caustics = min(tex1,tex2) * _CausticsStrength;
```
我们使用 min 操作来组合两个移动纹理，该操作将返回最小的输入。
强度参数 `_CausticsStrength` 相乘，控制焦散显示的亮度。

## RGB Split

穿过水面的光线会发生折射，在这个过程中，不同波长的光在不同的角度下发生折射，导致光线形成彩虹图案。
![[Pasted image 20230806161202.png]]

思路：创建将对焦散纹理采样3次的函数，每次都向 UV 添加偏移。然后，我们使用这3个样本作为最终结果的 rgb 分量。
```c
float3 SampleCaustics(float2 uv, float uvOffset)
{
    float2 uv1 = uv + float2(uvOffset, uvOffset);
    float2 uv2 = uv + float2(uvOffset, -uvOffset);
    float2 uv3 = uv + float2(-uvOffset, -uvOffset);

    float r = SAMPLE_TEXTURE2D(_CausticsTexture, sampler_CausticsTexture, uv1).r;
    float g = SAMPLE_TEXTURE2D(_CausticsTexture, sampler_CausticsTexture, uv2).r;
    float b = SAMPLE_TEXTURE2D(_CausticsTexture, sampler_CausticsTexture, uv3).r;

    return float3(r, g, b);
}

  
float3 tex1 = SampleCaustics(uv1, _CausticsUVOffset);  
float3 tex2 = SampleCaustics(uv2, _CausticsUVOffset);
```
[Believable Caustics Reflections - Alan Zucconi](https://www.alanzucconi.com/2019/09/13/believable-caustics-reflections/)
## 亮度遮罩Luminance mask
基于场景中的亮度遮罩焦散来改进焦散效果。这将使焦散在阴影区域中显得不那么突出。
```c
//亮度遮罩
float3 SceneColor = SAMPLE_TEXTURE2D(_CameraOpaqueTexture, sampler_CameraOpaqueTexture, ScreenUV).rgb;
float sceneLuminance = Luminance(SceneColor);
half luminanceMask = lerp(1, sceneLuminance, _CausticsLuminanceMask);
```
对场景颜色进行采样，计算亮度，然后使用遮罩强度参数创建遮罩。
由于亮度值几乎永远不会为零，焦散仍将显示在阴影区域，只是亮度较低。


![[Pasted image 20230806161355.png]]

如果确实不希望焦散显示在阴影中，可以使用一个阈值，其中只有当亮度超过某个值时才会显示焦散。
```c
half luminanceMask = smoothstep(_CausticsLuminanceMaskStrength, _CausticsLuminanceMaskStrength + 0.1, sceneLuminance);
```

另一个选项是对场景的阴影贴图进行采样，并以此方式遮罩焦散。这将导致阴影所在的位置出现硬剪切。

## 边缘消退
焦散在焦散体积的边缘具有硬截断。为了使过渡更加柔和，我们引入了一种软边渐变遮罩。
```c
 //边缘渐变
float edgeFadeMask = 1 - saturate((distance(rebuildPosOS, 0) - _CausticsFadeRadius) / (1 - _CausticsFadeStrength));
```

![[Pasted image 20230806163259.png]]
