# 案例
## ASE全息投影
投光：锥形模型+同形状贴图（PS画一个）+噪音
山：
1. 边缘光 
2. 线条（纹理采样或者用世界坐标） 
3. 扩散圈，从中间到边缘越来越暗。世界坐标求圈的范围
4. 从边缘到中心的高光
5. 线 ：mesh uv滚动  unity自带：Line Renderer
7. 球：边缘光
8. 红色区域：直接贴图或者使用一个建模

### 透明效果设置
![[Pasted image 20221006144843.png|300]]
### 锥体投影光
#### 透明度衰减
使用uv可以对水平和竖直方向进行透明度衰减![[Pasted image 20221007163814.png]]
#### 表面波动
这里使用的**Vronoi节点**可以进行多种效果变换。
[Voronoi](http://wiki.amplify.pt/index.php?title=Unity_Products:Amplify_Shader_Editor/Voronoi)![[Pasted image 20221007164401.png]]
#### 噪音跳动
Random Range节点：根据输入Seed返回一个伪随机数值，该值位于输入Min和Max分别定义的最小值和最大值之间![[Pasted image 20221007164610.png]]
不使用噪声纹理，直接使用uv坐标也可以实现![[Pasted image 20221007164814.png]]
### 山地
#### 菲涅尔边缘光
![[Pasted image 20221009230406.png]]
#### 方格线
基于顶点坐标的方格线![[Pasted image 20221007165548.png]]
#### 扫描
基于顶点与原点的距离：从中间到两边扫描![[Pasted image 20221007170254.png]]
基于世界坐标高度：从下到上![[Pasted image 20221007170619.png]]
### 发光组件
#### 闪烁
![[Pasted image 20221007170749.png]]
#### 线条移动
横向：使用unity内置object3d，Line![[Pasted image 20221007171035.png]]
旋转时钟：基于极坐标, 所用模型uv也是一圈![[Pasted image 20221007171829.png|200]]![[Pasted image 20221007171134.png]]
## 琥珀
### 制作流程
1.表现需求    如：琥珀材质（纯表现）
->合理？
2.渲染特性   如：漫反射、高光、反射、透射（与原画确认）
->可行性？
3.特性实现
如： （1）现有Shader能否满足？（PBR，UberEffect等）
	    （2）与环境的关系（天气变换，阴影，氛围，融合等）
	    （3）具体实现
->尽早反馈
4.迭代反馈
如：（1）反馈迭代
	（2）优化（性能要求，代码优化，贴图合并复用等）
### 渲染特性
![[Pasted image 20221009224517.png]]
                                     不均匀内部介质：视差贴图

ASE中CubeMap使用前的设置
![[Pasted image 20221010093658.png|300]]
![[Pasted image 20221010093722.png|300]]
蚊子先渲染，琥珀后渲染：将蚊子的渲染队列改小点1800
抓起已经渲染过的蚊子，再以屏幕空间采样的方式显示出来，还加上了折射
### 流程
#### 1.高亮折痕
![[Pasted image 20221015154226.png]]![[Pasted image 20221015154255.png]]
#### 2.CheapSSS透射
![[Pasted image 20221015154315.png]]
#### 3.Bubble 气泡
![[Pasted image 20221015154356.png]]![[Pasted image 20221015154406.png]]
#### 4. 湖泊底色
![[Pasted image 20221015154423.png]]
#### 5.  光照
![[Pasted image 20221015154444.png]]![[Pasted image 20221015154458.png]]
#### 6. MatCap
![[Pasted image 20221015154513.png]]
#### 7.CubeMap
![[Pasted image 20221015154529.png]]
#### 8.SceneTexColor
![[Pasted image 20221015154552.png]]
#### 9.SceneColor
![[Pasted image 20221015154618.png]]
#### 10.result
![[Pasted image 20221015154933.png]]
#### 代码
```c#
Shader "Unlit/code_amber"  
{  
    Properties  
    {  
        [Header(Texture)]  
        [Space(5)]  
        _MainTex ("MainTex", 2D) = "white" {}  
        _amber_n1 ("amber_n1", 2D) = "white" {}  //NormalMap  
        _amber_n2 ("amber_n2", 2D) = "white" {}  //NormalMap  
        _amber_bubble("amber_bubble", 2D) = "white" {}    
        _ambermix("amber_bubble", 2D) = "white" {}    
        _MatCap("MatCap",2D) = "white" {}  
        _CubeMap("CubeMap",Cube) = "white" {}  
        _normals_exporthr("normals_exporthr", 2D) = "white" {}  
          
        [Header(ParallaxMap)]  
        [Space(5)]  
        _ParallaxHeight("CreaseParallaxHeight",float) = 1.2  
        _ParallaxScale("CreaseParallaxScale",float) = 1  
        [Header(Crease)]  
        [Space(5)]  
        _CreaseFresnelScale("CreaseFresnelScale", Range(0,1)) = 0.1  
        _CreaseFresnelColor("CreaseFresnelColor", Color) = (1,1,1,1)  
          
        [Header(CheapSSS)]  
        [Space(5)]  
        _CheapSSSAngle ("CheapSSSAngle", Range(0,1)) = 0.3  
        _CheapSSSExp("CheapSSSExp",float) = 1  
        _CheapSSSScale("CheapSSSExp",float) = 1  
        _CheapSSSColor("CheapColor",Color) = (1,1,1,1)  
          
        [Header(BubbleParallaxUV)]  
        [Space(5)]  
        _BubbleParallaxUVHeight("BubbleParallaxUVHeight",float) = 1.2  
        _BubbleParallaxUVScale("BubbleParallaxUVScale",float) = 1.9  
        _BubbleParallaxUVOffset("BubbleParallaxUVOffset",float) = 4.43  
        _BubbleColor("BubbleColor",Color) = (1,1,1,1)  
          
        [Header(FlowNoise)]  
        [Space(5)]  
        _AmberColor("AmberColor",Color) = (1,1,1,1)  
        _lerpColor1("lerpColor1",Color) = (0.4,0.4,0.4,0)  
        _lerpColor2("lerpColor1",Color) = (1,1,1,0)  
          
        [Header(CubeMap)]  
        [Space(5)]  
        _CubeMapFresnelScale("CubeMapFresnelScale", Range(0,1)) = 0.1  
        _CubeMapFresnelIntensity("_CubeMapFresnelIntensity",float) =1  
        [Header(Specular)]  
        [Space(5)]  
        _SpecularExp("_SpecularExp",float) = 1  
        _SpecularScale("_SpecularScale",float) = 1  
        [Header(SceenTexColor)]  
        [Space(5)]  
        _SceenUVlerp("SceenUVlerp",Range(0,1)) = 0.8  
        _SceenUVScale("SceenUVScale",float) =1  
        _SceenTexColor("SceenUVColor",Color) = (1,1,1,1)  
          
        [Header(SceenColor)]  
        [Space(5)]  
        _RefractRatio("RefractRatio",Range(0,1)) = 0.6  
        _SceneDistortionIntensity("_SceneDistortionIntensity",Range(0,1)) = 0.05  
        _FinalColorAlpha("FinalColor_Alpha",Range(0,1)) = 0.5  
        }  
    SubShader  
    {  
        GrabPass{"_ScreenTex"}  
        Tags { "RenderType"="Opaque" }  
        LOD 100  
  
        Pass  
        {  
            CGPROGRAM  
            #pragma vertex vert  
            #pragma fragment frag  
  
            #include "UnityCG.cginc"  
            #include "AutoLight.cginc"  
  
            struct appdata  
            {  
                float4 vertex : POSITION;  
                float3 normal : NORMAL;  
                float4 tangent : TANGENT;  
                float2 uv : TEXCOORD0;  
            };  
  
            struct v2f  
            {  
                float4 vertex : SV_POSITION;  
                float2 uv : TEXCOORD0;  
                float3 normal : TEXCOORD1;  
                float3 tangent : TEXCOORD2;  
                float3 bitanget : TEXCOORD3;  
                float3 worldPosition : TEXCOORD4;  
                float3 localPosition : TEXCOORD5;  
                float3 localNormal : TEXCOORD6;  
                float4 ScreenPos : TEXCOORD7;  
                  
            };  
  
            sampler2D _MainTex;  
            float4 _MainTex_ST;  
            sampler2D _amber_n1;  
            sampler2D _amber_n2;  
            sampler2D _amber_bubble;  
            sampler2D _ambermix;  
            sampler2D  _MatCap;  
            samplerCUBE _CubeMap;  
            sampler2D _normals_exporthr;  
            sampler2D _ScreenTex;  
              
            //ParallaxMap  
            float _ParallaxHeight, _ParallaxScale;  
              
            //Crease  
            float _CreaseFresnelScale;  
            float4 _CreaseFresnelColor;  
  
            //CheapSSS  
            float _CheapSSSAngle;  
            float _CheapSSSExp;  
            float _CheapSSSScale;  
            float4 _CheapSSSColor;  
  
            //Bubble  
            float _BubbleParallaxUVHeight,_BubbleParallaxUVScale,_BubbleParallaxUVOffset;  
            float4  _BubbleColor;  
                  
            //FlowNoise  
            float4 _AmberColor,_lerpColor1,_lerpColor2;  
  
            //MatCap  
            float4 _MatCap_ST;  
              
            //CubeMap  
            float _CubeMapFresnelScale;  
            float _CubeMapFresnelIntensity;  
            //Specular  
            float _SpecularExp, _SpecularScale;  
  
            //SceneTexColor  
            float _SceenUVlerp;  
            float _SceenUVScale;  
            float4 _SceenTexColor;  
  
            //ScreenColor  
            float _RefractRatio;  
            float _SceneDistortionIntensity;  
  
            float  _FinalColorAlpha;  
            v2f vert (appdata v)  
            {  
                v2f o;  
                o.vertex = UnityObjectToClipPos(v.vertex);  
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);  
                o.normal = UnityObjectToWorldNormal(v.normal);  
                o.tangent = UnityObjectToWorldDir(v.tangent);  
                o.bitanget = cross(o.normal, o.tangent) * v.tangent.w;  
                o.worldPosition = mul(unity_ObjectToWorld, v.vertex);  
                o.localPosition = v.vertex.xyz;  
                o.localNormal = v.normal;  
                o.ScreenPos = ComputeScreenPos(o.vertex);  
                return o;  
            }  
  
            fixed4 frag (v2f i) : SV_Target  
            {  
                float3x3 TBN = float3x3(normalize(i.tangent),normalize(i.bitanget),normalize(i.normal));  
                float3 world_Normal = normalize(i.normal);  
                float3 world_LightDir = normalize(UnityWorldSpaceLightDir(i.worldPosition));  
                float3 world_ViewPos = normalize(UnityWorldSpaceViewDir(i.worldPosition));  
                //左乘TBN 从世界空间转换到切线空间  
                float3 tangent_ViewPos = normalize(mul(TBN, world_ViewPos));  
  
                //ParallaxMap  
                float2 ParrallaxMap = ((_ParallaxHeight - 1) * tangent_ViewPos.xy * _ParallaxScale) + i.uv;  
                  
                //高亮折痕 Crease  NormalMap + Fresnel                float3 CreaseNormalMap = normalize(mul(UnpackNormal(tex2D(_amber_n2,ParrallaxMap)), TBN));  
                float CreaseFresnel = _CreaseFresnelScale+ (1 -_CreaseFresnelScale) * pow(1 - saturate(dot(CreaseNormalMap, world_ViewPos)), 5);  
                float4 Crease = mul(CreaseFresnel, _CreaseFresnelColor);  
  
                //CheapSSS 透射  
                float3 CheapSSSNormalMap = normalize(mul(UnpackNormal(tex2D(_amber_n1,i.uv)), TBN));  
                float4 CheapSSSEdge = pow(dot(normalize(-lerp(CheapSSSNormalMap, world_LightDir, _CheapSSSAngle)),world_ViewPos),_CheapSSSExp) * _CheapSSSScale;  
                float4 CheapSSS = CheapSSSEdge * _CheapSSSColor;  
  
                //Bubble  
                //BubbleParallaxMap                float2 BubbleParrallaxMap = ((_BubbleParallaxUVHeight-1) * tangent_ViewPos.xy * _BubbleParallaxUVScale) + (i.uv * _BubbleParallaxUVOffset);  
  
                float3 BubbleTex = tex2D(_amber_bubble, BubbleParrallaxMap);  
                float4 Bubble = BubbleTex.g * _BubbleColor;  
  
                //FlowNoise 琥珀底色  
                float3 FlowNoiseMap = tex2D(_ambermix, ParrallaxMap);  
                float4 FlowNoise = lerp(_lerpColor1, _lerpColor2,FlowNoiseMap.g) * _AmberColor;  
                  
                //MatCap  
                float3 view_Normal = mul(unity_MatrixV, world_Normal);  
                float3 view_Normal01 = view_Normal * 0.5 + 0.5;  
                float3 MatCaptexture = tex2D(_MatCap, view_Normal01.xy);  
                float4 MatCap = float4(MatCaptexture,1);  
  
                //CubeMap  
                float3 Reflect = reflect(-world_ViewPos,world_Normal);  
                float3 CubeMaptexture = texCUBE(_CubeMap, Reflect);   
                float CubeMapFresnel = _CubeMapFresnelScale+ (1 -_CubeMapFresnelScale) * pow(1 - saturate(dot(CreaseNormalMap, world_ViewPos)), 5);  
                float4 CubeMap = float4(CubeMaptexture * CubeMapFresnel *_CubeMapFresnelIntensity,1);  
                  
                  
                //Diffuse  
                float4 Diffuse = dot(world_Normal,world_LightDir) * 0.5 + 0.5;  
                  
                //Specular  
                float3 SpecularNormalMap = normalize(mul(UnpackNormal(tex2D(_amber_n2, i.uv)), TBN));  
                float3 Half_Vector = normalize(world_ViewPos + world_LightDir);  
                float4 Specular = pow(max(0, dot(SpecularNormalMap,Half_Vector)),_SpecularExp) * _SpecularScale;  
  
                //SceneTexColor  
                float2 ScreenTexUV = lerp(i.uv,normalize(i.ScreenPos),_SceenUVlerp)* _SceenUVScale;  
                float4 SceneTexColor = tex2D(_amber_bubble, ScreenTexUV).r * _SceenTexColor;  
  
                //SceneColor 折射  
                float3 SceneColorNormalMap = normalize(mul(UnpackNormal(tex2D(_normals_exporthr,i.uv)), TBN));  
                float3 Refract = refract(-world_ViewPos,SceneColorNormalMap,_RefractRatio);   
                float3 tangent_Refract = normalize(mul(TBN, Refract));  
                float4 SceneColor = tex2D(_ScreenTex, (normalize(i.ScreenPos) + tangent_Refract * _SceneDistortionIntensity));  
                  
                float4 finalColor = lerp(SceneColor,Crease+Bubble+FlowNoise+MatCap+CubeMap+Specular + SceneTexColor, _FinalColorAlpha);  
                  
                return finalColor;  
            }  
            ENDCG  
        }  
    }  
}
```





# 【第二章】光照模型

![[Pasted image 20221005153922.png]]![[Pasted image 20221005160726.png]]![[Pasted image 20221005160805.png]]![[Pasted image 20221005231913.png]]

```less
float3 world_LightDir = normalize(UnityWorldSpaceLightDir(i.worldPosition));
//通过这个求得的是顶点指向光源的方向，使用reflect函数时要取反
//reflect(i,n)
//i是入射方向（光指向顶点的方向），n是法线方向。
float world_Reflect = reflect(-world_LightDir, world_Normal);
```

## ASE实现
### 输入
注意设置成normalize![[Pasted image 20221007220753.png]]
### 要注意的问题
1. 单位问题，向量点积得到的都是常数，而不是向量
2. lambert/phong/BlinnPhong 点积的结果要使用max（0，x）限制
3. 运用自定义函数控制光照结果，并且没有控制光照颜色，并不是很严谨。更严谨的运算参考入门精要。本课程的方法仍符合原理。
### 自定义函数
用于参数控制，先幂再乘
![[Pasted image 20221005220249.png]]
### 漫反射
#### Lambert
![[Pasted image 20221005214517.png]]
#### Half Lambert
![[Pasted image 20221005214604.png]]
#### WarpLight
不常用
![[Pasted image 20221005214627.png]]
#### BandedLight
![[Pasted image 20221005215351.png|300]]
条带化效果常用于卡渲，一般分两段
![[Pasted image 20221005214707.png]]
![[Pasted image 20221005214731.png]]
### 高光
#### Phong
![[Pasted image 20221007222614.png]]
#### Blinn Phong
![[Pasted image 20221007222630.png]]
### 背光cheapSSS
![[Pasted image 20221005215611.png|300]]
![[Pasted image 20221005214850.png]]

### 输出
![[Pasted image 20221007223010.png]]
## 代码实现
![[Pasted image 20221007221541.png]]

```c++
Shader "Unlit/NewUnlitShader"  
{  
    Properties  
    {  
        _MainTex ("Texture", 2D) = "white" {}  
        _Value("Value", Float) = 1  
        _Color("Color",Color) = (1,1,1,1)  
          
        [Space(10)]   
        _PhongExp("PhongExp",Float) = 1  
        _PhongScale("PhongScale",Float) = 1  
        [Space(10)]   
        _BlinnPhongExp("BlinnPhongExp",Float) = 1  
        _BlinnPhongScale("BlinnPhongScale",Float) = 1  
        _WrapValue("WrapValue",Float) =1  
        [Space(10)]  //属性面板加几个空格，用于排版  
        _CheapSSSValue("CheapSSSValue",Float) =0.5  
        _CheapSSSExp("CheapSSSPhongExp",Float) = 1  
       _CheapSSSScale("CheapSSSPhongScale",Float) = 1  
        }  
    SubShader  
    {  
        Tags { "RenderType"="Opaque" "LightMode" = "ForwardBase" "Queue" = "Geometry"}  
          
        Pass  
        {  
            CGPROGRAM  
            #pragma vertex vert  
            #pragma fragment frag  
  
            #include "UnityCG.cginc"  
            #include "AutoLight.cginc"  
            struct appdata  
            {  
                float4 vertex       : POSITION;  
                float2 uv1           : TEXCOORD0;  
                float2 uv2          : TEXCOORD1;  
                float3 normal       : NORMAL;  
                float4 tangent      : TANGENT;  
                float4 vertexColor  : COLOR;  
            };  
  
            struct v2f  
            {  
                float4 pos           : SV_POSITION;  
                float2 uv1            : TEXCOORD0;  
                float3 normal        : TEXCOORD1;  
                float3 tangent       : TEXCOORD2;  
                float3 bitangent     : TEXCOORD3;  
                float3 worldPosition : TEXCOORD4;  
                float3 localPosition : TEXCOORD5;  
                float3 localNormal   : TEXCOORD6;  
                float4 vertexColor   : TEXCOORD7;  
                float2 uv2           : TEXCOORD8;  
            };  
  
            sampler2D _MainTex;  
            float4 _MainTex_ST;  
            float _PhongExp,_PhongScale;  
            float _BlinnPhongExp,_BlinnPhongScale;  
            float _WrapValue;  
            float _CheapSSSValue,_CheapSSSExp,_CheapSSSScale;  
              
            v2f vert(appdata v)  
            {  
                v2f o;  
                o.pos = UnityObjectToClipPos(v.vertex);  
                  
                o.uv1 = TRANSFORM_TEX(v.uv1, _MainTex); 
                o.uv2 =  TRANSFORM_TEX(v.uv2, _MainTex); 
                o.normal = UnityObjectToWorldNormal(v.normal);  
                o.worldPosition = mul(unity_ObjectToWorld, v.vertex);  
                o.localPosition = v.vertex.xyz;  
                o.tangent = UnityObjectToWorldDir(v.tangent);  
                o.bitangent = cross(o.normal, o.tangent) * v.tangent.w;;  
                o.localNormal = v.normal;  
                o.vertexColor = v.vertexColor;  
  
                return o;  
            }  
  
            float4 frag(v2f i) : SV_Target  
            {  
                float3 world_Normal = normalize(i.normal);  
                float3 world_LightDir = normalize(UnityWorldSpaceLightDir(i.worldPosition));  
                float3 world_ViewPos = normalize(UnityWorldSpaceViewDir(i.worldPosition));  
                //注意第一个参数为负数  
                float3 world_Reflect = normalize(reflect(-world_LightDir, world_Normal));  
                float3 world_HalfVector = normalize(world_LightDir + world_ViewPos);  
  
                //纹理采样  albedo:反射率 注意这里给的源氏模型必须用uv2  
                float4 albedo = tex2D(_MainTex, i.uv2);  
                  
                //Diffuse  
                float NL = dot(world_Normal ,world_LightDir);  
                float Lambert = max(0, NL);      
                float Half_Lambert = NL * 0.5 + 0.5;      
                float BandedLight = floor((NL * 0.5 + 0.5) *4)/4;      
  
                //BackLight  
                //沿着光线方向偏移法线，最后再取反  
                float3 N_Shift = -normalize(world_Normal * _CheapSSSValue + world_LightDir);  
                float BackLight = pow(saturate(dot(N_Shift, world_ViewPos)),_CheapSSSExp) * _CheapSSSScale;  
                  
                //Specular  
                float VR = dot(world_ViewPos, world_Reflect);  
                float phong = pow(max(0, VR), _PhongExp) * _PhongScale;  
                float NH = dot(world_Normal, world_HalfVector);  
                float BlinnPhong = pow(max(0, NH), _BlinnPhongExp) * _BlinnPhongScale;  
                  
                float4 Ambient = UNITY_LIGHTMODEL_AMBIENT;  
                float4 Diffuse = Half_Lambert;  
                float4 Specular = BlinnPhong;  
                float4 FinalColor = (Ambient+ Diffuse + BackLight) * albedo + Specular;  
                  
                return FinalColor;  
            }  
            ENDCG  
        }  
    }  
}
```

